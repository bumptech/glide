import com.android.build.gradle.api.LibraryVariant

apply plugin: 'java'

// The paths of Android projects that should be included only in Javadoc, not in the jar.
static def getAndroidPathsForJavadoc() {
    [
            ':integration:concurrent',
            ':integration:gifencoder',
            ':integration:okhttp',
            ':integration:okhttp3',
            ':integration:recyclerview',
            ':integration:volley',
            ':library',
            ':mocks',
            ':third_party:disklrucache',
            ':third_party:gif_decoder',
    ]
}

// The paths of Java projects that should be included only in Javadoc, not in the jar.
static def getJavaPathsForJavadoc() {
    [':annotation']
}

(getAndroidPathsForJavadoc() + getJavaPathsForJavadoc()).each {
    evaluationDependsOn(it)
}

def asProjects(paths) {
    paths.collect { String path -> project(path) }
}

def getAndroidSdkDirectory() {
    project(':library').android.sdkDirectory
}

def getAndroidCompileSdkVersion() {
    project(':library').android.compileSdkVersion
}

def getAndroidProjectsForJavadoc() {
    asProjects(getAndroidPathsForJavadoc())
}

def getAndroidLibraryVariantsForJavadoc() {
    getAndroidProjectsForJavadoc().collect { project ->
        project.android.libraryVariants.findAll { type ->
            type.buildType.name.equalsIgnoreCase("debug")
        }
    }.sum()
}

def getSourceFilesForJavadoc() {
    getAndroidProjectsForJavadoc().collect { project ->
      project.android.sourceSets.main.java.srcDirs
    }
}

def getAndroidJar() {
    "${getAndroidSdkDirectory()}/platforms/${getAndroidCompileSdkVersion()}/android.jar"
}

project.archivesBaseName = "${POM_ARTIFACT_ID}-${VERSION_NAME}"

// Generate javadocs and sources containing batched documentation and sources for all internal
// projects.
def javadocTask = tasks.create("debugJavadoc", Javadoc) {
    source = getSourceFilesForJavadoc()

    doFirst {
        it.classpath =
                project.files(
                        getAndroidJar(),
                        getAndroidLibraryVariantsForJavadoc().collect {
                            LibraryVariant lib ->
                                lib.getJavaCompileProvider().get().classpath.files
                        },
                        // Finds dependencies of Android packages that would otherwise be
                        // ignored (Volley in particular)
                        getAndroidProjectsForJavadoc().collect { Project project ->
                            project.file('build/intermediates/classes/debug')
                        }
        )
    }

    options {
        links("http://docs.oracle.com/javase/7/docs/api/")
        links("https://square.github.io/okhttp/3.x/okhttp/")
        links("https://square.github.io/okhttp/2.x/okhttp/")
        linksOffline("http://d.android.com/reference",
                "${getAndroidSdkDirectory()}/docs/reference")
    }

    exclude '**/R.java'
}

def cleanJavadocTask = task("cleanDebugJavadoc", type: Delete) {
    delete javadocTask.destinationDir
} as Task
clean.dependsOn(cleanJavadocTask)

def javadocJarTask = task("debugJavadocJar", type: Jar) {
    from javadocTask.destinationDir
} as Task

javadocJarTask.dependsOn(javadocTask)

(getAndroidProjectsForJavadoc()).each {
    project ->
        debugJavadoc.dependsOn(project.tasks.compileDebugSources)
        jar.dependsOn(project.tasks.compileDebugSources)
}


jar {
    from files(
            getAndroidLibraryVariantsForJavadoc().collect { LibraryVariant variant ->
                variant.getJavaCompileProvider().get().destinationDir
            }
    )
    exclude "**/R.class"
    exclude "**/R\$*.class"
    exclude "android/**"
}
 


artifacts {
    archives debugJavadocJar {
        classifier 'javadoc'
    }
}

