apply plugin: 'com.android.application'

dependencies {
    implementation project(':library')
    implementation(project(':integration:recyclerview')) {
        transitive = false
    }
    annotationProcessor project(':annotation:compiler')

    implementation "androidx.appcompat:appcompat:${ANDROID_X_APPCOMPAT_VERSION}"
    implementation "com.android.volley:volley:${VOLLEY_VERSION}"
    implementation "androidx.recyclerview:recyclerview:${ANDROID_X_RECYCLERVIEW_VERSION}"
}

android {
    compileSdk COMPILE_SDK_VERSION as int

    defaultConfig {
        applicationId 'com.bumptech.glide.samples.flickr'
        minSdk MIN_SDK_VERSION as int
        targetSdk TARGET_SDK_VERSION as int

        versionCode 1
        versionName '1.0'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

task run(type: Exec, dependsOn: 'installDebug') {
    description 'Installs the APK and runs the main activity: "gradlew :samples:???:run"'
    commandLine "${android.sdkDirectory}/platform-tools/adb", 'shell', 'am', 'start', '-n', 'com.bumptech.glide.samples.flickr/.FlickrSearchActivity'
}
