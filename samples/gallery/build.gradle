apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "org.jetbrains.kotlin.plugin.parcelize"
apply plugin: 'com.google.devtools.ksp'

dependencies {
    implementation project(':library')
    implementation project(':integration:compose')
    implementation(project(':integration:recyclerview')) {
        transitive = false
    }

    implementation "androidx.recyclerview:recyclerview:$ANDROID_X_RECYCLERVIEW_VERSION"
    implementation "androidx.fragment:fragment-ktx:$ANDROID_X_FRAGMENT_VERSION"
    implementation "androidx.core:core-ktx:$ANDROID_X_CORE_KTX_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$JETBRAINS_KOTLINX_COROUTINES_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$JETBRAINS_KOTLINX_COROUTINES_VERSION"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$JETBRAINS_KOTLIN_VERSION"
    implementation "androidx.compose.foundation:foundation:$ANDROID_X_COMPOSE_FOUNDATION_VERSION"
    implementation "androidx.compose.ui:ui:$ANDROID_X_COMPOSE_VERSION"

    ksp project(':annotation:ksp')
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

android {
    compileSdk 33

    defaultConfig {
        applicationId 'com.bumptech.glide.samples.gallery'
        minSdk 29
        targetSdk 33
        versionCode 1
        versionName '1.0'
    }
    buildFeatures {
        compose = true
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.2.0'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

task run(type: Exec, dependsOn: 'installDebug') {
    description 'Installs the APK and runs the main activity: "gradlew :samples:???:run"'
    commandLine "${android.sdkDirectory}/platform-tools/adb", 'shell', 'am', 'start', '-n', 'com.bumptech.glide.samples.gallery/.MainActivity'
}
