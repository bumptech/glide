apply plugin: 'com.android.application'

dependencies {
    implementation project(':library')
    implementation(project(':integration:recyclerview')) {
        transitive = false
    }
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation "androidx.recyclerview:recyclerview:${ANDROID_X_RECYCLERVIEW_VERSION}"
    implementation "androidx.fragment:fragment:${ANDROID_X_FRAGMENT_VERSION}"
    annotationProcessor project(':annotation:compiler')
}

android {
    compileSdk COMPILE_SDK_VERSION as int

    defaultConfig {
        applicationId 'com.bumptech.glide.samples.giphy'
        minSdk MIN_SDK_VERSION as int
        targetSdk TARGET_SDK_VERSION as int
        versionCode 1
        versionName '1.0'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    namespace 'com.bumptech.glide.samples.giphy'
}

task run(type: Exec, dependsOn: 'installDebug') {
    description 'Installs the APK and runs the main activity: "gradlew :samples:???:run"'
    commandLine "${android.sdkDirectory}/platform-tools/adb", 'shell', 'am', 'start', '-n', 'com.bumptech.glide.samples.giphy/.MainActivity'
}
